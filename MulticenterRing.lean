import Mathlib.RingTheory.Ideal.Maps
import Mathlib.Algebra.DirectSum.Basic
import lemma
import Mathlib.RingTheory.Ideal.Operations

import Family
import Mathlib.Algebra.Algebra.Tower

suppress_compilation

open DirectSum Family

universe u

section defs

variable (A : Type (u+1)) [CommSemiring A]

@[ext]
structure Multicenter : Type (u+1) where
  (index : Type)
  (ideal : index → Ideal A)
  (elem : index → A)

end defs

namespace Multicenter

section semiring

variable {A : Type _} [CommSemiring A] (F : Multicenter A)

@[simps]
def ofFamily (index : Type) (c : index → A) : Multicenter A where
  index := index
  ideal i := Ideal.span {c i}
  elem i := c i

def LargeIdeal (i : F.index) : Ideal A := F.ideal i + Ideal.span {F.elem i}

lemma elem_mem_LargeIdeal (i: F.index) : F.elem i ∈ F.LargeIdeal i := by
  suffices inequality : Ideal.span {F.elem i} ≤ F.LargeIdeal i by
   apply inequality
   exact Ideal.mem_span_singleton_self (F.elem i)
  simp only [LargeIdeal, Submodule.add_eq_sup, le_sup_right]

abbrev prodLargeIdealPower (v : F.index →₀ ℕ) : Ideal A :=
  v.prod fun i k ↦ F.LargeIdeal i ^ k

structure PreDil where
  pow : F.index →₀ ℕ
  num : A
  num_mem : num ∈ F.LargeIdeal ^ pow

def r : F.PreDil → F.PreDil → Prop := fun x y =>
  ∃ β : F.index →₀ ℕ, x.num * F.elem^(β + y.pow) = y.num * F.elem^(β + x.pow)

variable {F}

lemma r_refl (x : F.PreDil) : F.r x x := by simp [r]

lemma r_symm (x y : F.PreDil) : F.r x y → F.r y x := by
  intro h
  rcases h with ⟨β , hβ⟩
  use β
  rw[hβ.symm]

lemma r_trans (x y z : F.PreDil) : F.r x y → F.r y z → F.r x z := by
  intro h g
  rcases h with ⟨β , hβ⟩
  rcases g with ⟨γ , gγ⟩
  have eq' := congr($hβ * F.elem^(γ+z.pow))
  have eq'' := congr($gγ * F.elem^(β+x.pow))
  use β+γ+y.pow
  simp only [← familyPow_add, ← mul_assoc] at eq' eq'' ⊢
  rw [show β + γ + y.pow + z.pow = (β + y.pow) + (γ + z.pow) by abel,
    familyPow_add, ← mul_assoc, hβ, mul_assoc, mul_comm (F.elem^(_ : F.index →₀ ℕ)), ← mul_assoc, gγ,
    mul_assoc, ← familyPow_add]
  congr 2
  abel

def setoid : Setoid (F.PreDil) where
  r := F.r
  iseqv :=
  { refl := r_refl
    symm {x y} := r_symm x y
    trans {x y z} := r_trans x y z }

variable (F) in
def Dilatation := Quotient F.setoid

scoped notation:max ring"["multicenter"]" => Dilatation (A := ring) multicenter
namespace Dilatation

def mk (x : F.PreDil) : A[F] := Quotient.mk _ x

lemma mk_eq_mk (x y : F.PreDil) : mk x = mk y ↔ F.r x y := by
  erw [Quotient.eq]
  rfl

@[elab_as_elim]
lemma induction_on {C : A[F] → Prop} (x : A[F]) (h : ∀ x : F.PreDil, C (mk x)) : C x := by
  induction x using Quotient.inductionOn with | h a =>
  exact h a

def descFun {B : Type*} (f : F.PreDil → B) (hf : ∀ x y, F.r x y → f x = f y) : A[F] → B :=
  Quotient.lift f hf

def descFun₂ {B : Type*} (f : F.PreDil → F.PreDil → B)
    (hf : ∀ a b x y, F.r a b → F.r x y → f a x = f b y) :
    A[F] → A[F] → B :=
  Quotient.lift₂ f <| fun a x b y ↦ hf a b x y

@[simp]
lemma descFun_mk {B : Type*} (f : F.PreDil → B) (hf : ∀ x y, F.r x y → f x = f y) (x : F.PreDil) :
    descFun f hf (mk x) = f x := rfl

@[simp]
lemma descFun₂_mk_mk {B : Type*} (f : F.PreDil → F.PreDil → B)
    (hf : ∀ a b x y, F.r a b → F.r x y → f a x = f b y) (x y : F.PreDil) :
    descFun₂ f hf (mk x) (mk y) = f x y := rfl

@[simps]
def add' (x y : F.PreDil) : F.PreDil where
 pow := x.pow + y.pow
 num := F.elem ^ y.pow * x.num + F.elem ^ x.pow * y.num
 num_mem := Ideal.add_mem _
  (by
    rw [add_comm, familyPow_add]
    exact Ideal.mul_mem_mul (Ideal.mem_familyPow_of_mem fun i _ ↦ elem_mem_LargeIdeal F i)
      x.num_mem)
  (by
    rw [familyPow_add]
    exact Ideal.mul_mem_mul (Ideal.mem_familyPow_of_mem fun i _ ↦ elem_mem_LargeIdeal F i)
      y.num_mem)

instance : Add A[F] where
  add := descFun₂ (fun x y ↦ mk (add' x y))  <| by
   rintro x y x' y' ⟨α, hα⟩ ⟨β, hβ⟩
   have eq := congr($hβ * F.elem^(x.pow + y.pow + α))
   have eq' := congr($hα * F.elem^(x'.pow + y'.pow + β))
   have eq'' := congr($eq + $eq')
   simp only
   rw [mk_eq_mk]
   use α + β
   simp only [mul_assoc, ← familyPow_add] at eq''
   simp only [add'_num, add'_pow, add_mul]
   rw [mul_comm _ x.num, mul_comm _ x'.num, mul_assoc, ← familyPow_add,
    mul_assoc, ← familyPow_add]
   rw [mul_comm _ y.num, mul_comm _ y'.num, mul_assoc, ← familyPow_add,
    mul_assoc, ← familyPow_add]
   convert eq'' using 1 <;>
   · rw [add_comm]
     congr 3 <;> abel

lemma mk_add_mk (x y : F.PreDil) : mk x + mk y = mk (add' x y) := rfl

@[simps]
def mul' (x y : F.PreDil) : F.PreDil where
  pow := x.pow + y.pow
  num := x.num * y.num
  num_mem := Ideal.mem_familyPow_add x.num_mem y.num_mem

lemma dist' (x y z : F.PreDil) : F.r (mul' x (add' y z))
                                (add' (mul' x y) (mul' x z))  := by
  use 0
  simp [familyPow_add]
  ring

instance : Mul A[F] where
  mul := descFun₂ (fun x y ↦ mk <| mul' x y) <| by
    rintro a b x y ⟨α, hα⟩ ⟨β, hβ⟩
    rw [mk_eq_mk]
    use α + β
    simp only [mul'_num, mul'_pow]
    rw [show α + β + (b.pow + y.pow) = (α + b.pow) + (β + y.pow) by abel, familyPow_add,
      show a.num * x.num * (F.elem^(α + b.pow) * F.elem^(β + y.pow)) =
        (a.num * F.elem^(α + b.pow)) * (x.num * F.elem^(β + y.pow)) by ring, hα, hβ,
      show b.num * F.elem^(α + a.pow) * (y.num * F.elem^(β + x.pow)) =
        b.num * y.num * (F.elem^(α + a.pow) * F.elem^(β + x.pow)) by ring, ← familyPow_add]
    congr 2
    abel

lemma mk_mul_mk (x y : F.PreDil) : mk x * mk y = mk (mul' x y) := rfl

instance : Zero A[F] where
  zero := mk {
    pow := 0
    num := 0
    num_mem := by exact Submodule.zero_mem (F.prodLargeIdealPower 0)
  }

lemma zero_def :  (0 :A[F]) =  (mk {
    pow := 0
    num := 0
    num_mem := by simp only [Finsupp.prod_zero_index, Ideal.one_eq_top, Submodule.zero_mem]
  } :A[F]):= rfl

instance : One A[F] where
  one := mk {
    pow := 0
    num := 1
    num_mem := by exact Submodule.one_le.mp fun ⦃x⦄ a ↦ a
  }

lemma one_def :  (1 :A[F]) =  (mk {
  pow := 0
  num := 1
  num_mem := by simp
} :A[F]):= rfl

instance : AddCommMonoid A[F] where
  add_assoc := by
   intro a b c
   induction a using induction_on with |h x =>
   induction b using induction_on with |h y =>
   induction c using induction_on with |h z =>
    simp only [mk_add_mk, mk_eq_mk]
    use 0
    simp only [add'_num, add'_pow, familyPow_add, zero_add]
    ring
  zero_add := by
   intro a
   induction a using induction_on with |h x=>
    simp only [zero_def, mk_add_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]
  add_zero := by
   intro a
   induction a using induction_on with |h x=>
    simp only [zero_def, mk_add_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]
  add_comm := by
   intro a b
   induction a using induction_on with |h x =>
   induction b using induction_on with |h y =>
    simp only [mk_add_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]
    ring
  nsmul := nsmulRec

instance monoid : Monoid A[F] where
  mul_assoc := by
   intro a b c
   induction a using induction_on with |h x =>
   induction b using induction_on with |h y =>
   induction c using induction_on with |h z =>
    simp only [mk_mul_mk, mk_eq_mk]
    use 0
    simp only [mul'_num, mul'_pow, zero_add, familyPow_add]
    ring
  one_mul := by
   intro a
   induction a using induction_on with |h x =>
    simp only [one_def, mk_mul_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]
  mul_one := by
   intro a
   induction a using induction_on with |h x =>
    simp only [one_def, mk_mul_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]

instance instCommSemiring : CommSemiring A[F] where
  __ := monoid
  left_distrib := by
   rintro a b c
   induction a using induction_on with |h x =>
   induction b using induction_on with |h y =>
   induction c using induction_on with |h z =>
    simp only [mk_add_mk, mk_mul_mk, mk_eq_mk]
    use 0
    simp only [mul'_num, add'_num, add'_pow, mul'_pow, zero_add, familyPow_add]
    ring
  right_distrib := by
   rintro a b c
   induction a using induction_on with |h x =>
   induction b using induction_on with |h y =>
   induction c using induction_on with |h z =>
    simp only [mk_add_mk, mk_mul_mk, mk_eq_mk]
    use 0
    simp only [mul'_num, add'_num, add'_pow, mul'_pow, zero_add, familyPow_add]
    ring
  zero_mul := by
   rintro a
   induction a using induction_on with |h x =>
    simp only [zero_def, mk_mul_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]
  mul_zero := by
   rintro a
   induction a using induction_on with |h x =>
    simp only [zero_def, mk_mul_mk, mk_eq_mk]
    use 0
    simp [familyPow_add]

  mul_comm := by
   intro a b
   induction a using induction_on with |h x =>
   induction b using induction_on with |h y =>
    simp only [mk_mul_mk, mk_eq_mk]
    use 0
    simp only [mul'_num, mul'_pow, zero_add, familyPow_add]
    ring

variable (F) in
@[simps]
def fromBaseRing : A →+* A[F] where
  toFun x := .mk
        { pow := 0
          num := x
          num_mem := by simp }
  map_one' := by simp [one_def]
  map_mul' _ _ := by simp only [mk_mul_mk, mk_eq_mk]; use 0; simp
  map_zero' := by simp [zero_def]
  map_add' _ _ := by simp [mk_add_mk, mk_eq_mk]; use 0; simp

instance instAlgebra {B : Type _} [CommSemiring B] [Algebra A B] (G : Multicenter B) : Algebra A B[G] :=
  RingHom.toAlgebra (RingHom.comp (fromBaseRing G) (algebraMap A B))

lemma algebraMap_eq : (algebraMap A A[F]) = fromBaseRing F := rfl

lemma algebraMap_apply (x : A) : algebraMap A A[F] x = mk {
  pow := 0
  num := x
  num_mem := by simp
} := rfl

lemma smul_mk (x : A) (y : F.PreDil) : x • mk y = mk {
    pow := y.pow
    num := x * y.num
    num_mem := Ideal.mul_mem_left _ _ y.num_mem } := by
  simp only [Algebra.smul_def, algebraMap_apply, mk_mul_mk, mk_eq_mk]
  use 0
  simp

abbrev frac (ν : F.index →₀ ℕ)  (m: F.LargeIdeal^ν) : A[F]:=
  mk {
    pow := ν
    num := m
    num_mem := by simp
    }

scoped notation:max m"/.[" F"]"ν => frac (F := F) ν m

scoped notation:max m"/."ν => frac ν m

lemma frac_add_frac (v w : F.index →₀ ℕ) (m : F.LargeIdeal^v) (n : F.LargeIdeal^w) :
    (m/.v) + (n/.w) =
    (⟨(m : A) * F.elem^w + (n : A) * F.elem^v, Ideal.add_mem _
      (Ideal.mem_familyPow_add m.2 (Ideal.mem_familyPow_of_mem fun i _ ↦ elem_mem_LargeIdeal F i))
      (add_comm v w ▸
        Ideal.mem_familyPow_add n.2 (Ideal.mem_familyPow_of_mem fun i _ ↦ elem_mem_LargeIdeal F i))⟩) /. (v + w) := by
  simp only [frac, mk_add_mk, mk_eq_mk]
  use 0
  simp only [add'_num, zero_add, familyPow_add, add'_pow]
  ring

lemma frac_mul_frac (v w : F.index →₀ ℕ) (m : F.LargeIdeal^v) (n : F.LargeIdeal^w) :
    (m/.v) * (n/.w) =
    (⟨m.1 * n, Ideal.mem_familyPow_add m.2 n.2⟩)/.(v + w) := by
  simp only [frac, mk_mul_mk, mk_eq_mk]
  use 0
  simp

lemma smul_frac (a : A) (v : F.index →₀ ℕ) (m : F.LargeIdeal^v) : a • (m/.v) = (a • m)/.v := by
  simp only [frac, smul_mk, mk_eq_mk]
  use 0
  simp

lemma nonzerodiv_image (v : F.index →₀ ℕ) : algebraMap A A[F] (F.elem^v) ∈ nonZeroDivisors A[F] := by
    simp only [nonZeroDivisors, Submonoid.mem_inf, mem_nonZeroDivisorsLeft_iff, mul_comm,
      mem_nonZeroDivisorsRight_iff, and_self]
    intro x h
    induction x using induction_on with |h x =>
    simp only [algebraMap_apply, mk_mul_mk, zero_def, mk_eq_mk] at h
    rcases h with ⟨ α, hα ⟩
    simp only [mul'_num, add_zero, mul'_pow, zero_mul] at hα
    simp only [zero_def, mk_eq_mk]
    use v + α
    simp [familyPow_add, ← mul_assoc, hα]

lemma image_elem_LargeIdeal_equal  (v : F.index →₀ ℕ) :
 Ideal.span ({algebraMap A A[F] (F.elem^v)}) =
    Ideal.map (algebraMap A A[F]) (F.LargeIdeal^v):= by
    refine le_antisymm ?_  ?_
    · rw [Ideal.span_le]
      simp only [Set.singleton_subset_iff, SetLike.mem_coe]
      apply Ideal.mem_map_of_mem
      apply Ideal.mem_familyPow_of_mem
      intros
      exact elem_mem_LargeIdeal F _
    · rw [Ideal.map_le_iff_le_comap]
      intro x hx
      have eq: algebraMap A A[F] x =
       algebraMap A A[F] (F.elem^v) * ⟨ x , hx⟩  /.v := by
       simp  [algebraMap_apply, frac, mk_mul_mk, mk_eq_mk]
       use 0
       simp [mul_comm]
      simp only [Ideal.mem_comap]
      rw [eq]
      apply Ideal.mul_mem_right
      apply Ideal.subset_span
      simp only [Set.mem_singleton_iff]

end Dilatation

end semiring

section ring

namespace Dilatation

variable {A : Type _} [CommRing A] {F : Multicenter A}

@[simps]
def neg' (x : F.PreDil) : F.PreDil where
  pow := x.pow
  num := -x.num
  num_mem := neg_mem x.num_mem

instance : Neg A[F] where
  neg := descFun (mk ∘ neg') <| by
    rintro x y ⟨α, hα⟩
    simp only [Function.comp_apply, mk_eq_mk]
    use α
    simp [hα]

lemma mk_neg (x : F.PreDil) : -mk x = mk (neg' x) := rfl

instance : CommRing A[F] where
  __ := instCommSemiring
  zsmul := zsmulRec
  neg_add_cancel := by
    intro a
    induction a using induction_on with |h x =>
    simp only [mk_neg, mk_add_mk, zero_def, mk_eq_mk]
    use 0
    simp

lemma neg_frac (v : F.index →₀ ℕ) (m : F.LargeIdeal^v) : -(m/.v) = (-m)/.v := by
  simp only [frac, mk_neg, mk_eq_mk]
  use 0
  simp

end Dilatation

end ring

section universal_property

variable {A B : Type _} [CommRing A] [CommRing B] (F : Multicenter A)

lemma  cond_univ_implies_large_cond [Algebra A B]
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)):
    (∀ (ν : F.index →₀ ℕ) , (Ideal.span {(algebraMap A B) (F.elem^ν)} = Ideal.map (algebraMap A B) (F.LargeIdeal^ν))) :=by
     classical
     intro v
     simp only [familyPow_def, Finsupp.prod, map_prod, map_pow]
     rw [Ideal.prod_span']
     simp [← Ideal.span_singleton_pow, gen]
     simp [Ideal.prod_map, Ideal.map_pow]

lemma equ_trivial_image_divisor_ring  [Algebra A B]  :
 ∀ i, Ideal.map (algebraMap A B) (Ideal.span {F.elem i})=
      Ideal.span {(algebraMap A B) (F.elem i)} := by
      intro i
      rw [Ideal.map_span, Set.image_singleton]

lemma equiv_small_big_cond [Algebra A B]  :
( ∀ i, Ideal.map (algebraMap A B) (Ideal.span {F.elem i}) = Ideal.map (algebraMap A B) (F.LargeIdeal i)) ↔
( ∀ i, Ideal.map (algebraMap A B) (Ideal.span {F.elem i}) ≥  Ideal.map (algebraMap A B) (F.ideal i)) := by
  constructor
  · intro h i
    have eq1 : (F.LargeIdeal i) ≥ (F.ideal i)  := by
      simp [LargeIdeal]
    have eq2 : Ideal.map (algebraMap A B) (F.LargeIdeal i) ≥
               Ideal.map (algebraMap A B) (F.ideal i) := by
                simp[Ideal.map_mono, eq1]
    simp[eq2, h]

  · intro h i
    have eq1: Ideal.map (algebraMap A B) (F.LargeIdeal i)=
               Ideal.map (algebraMap A B) (F.ideal i)+
               Ideal.map (algebraMap A B) (Ideal.span {F.elem i}):= by
               simp[LargeIdeal]
               rw [Ideal.map_sup]
    have eq2: Ideal.map (algebraMap A B) (Ideal.span {F.elem i})
             ≥  Ideal.map (algebraMap A B) (F.LargeIdeal i) := by
             simp[eq1, h]
    have eq3: Ideal.map (algebraMap A B) (Ideal.span {F.elem i})
             ≤   Ideal.map (algebraMap A B) (F.LargeIdeal i) := by
             simp[LargeIdeal, eq1, Ideal.map_sup]
    have eq4: Ideal.map (algebraMap A B) (Ideal.span {F.elem i})
             = Ideal.map (algebraMap A B) (F.LargeIdeal i) := by
             exact LE.le.antisymm' eq2 eq3
    exact eq4

lemma  lemma_exists_in_image [Algebra A B]
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)):
    (∀(ν : F.index →₀ ℕ) (m : F.LargeIdeal^ν) ,  (∃! bm : B ,  (algebraMap A B) (F.elem^ν) *bm=(algebraMap A B) (m) )):= by
      intro v m
      have mem : (algebraMap A B) m ∈  (F.LargeIdeal^v).map (algebraMap A B) := by
          apply Ideal.mem_map_of_mem
          exact m.2
      rw[← cond_univ_implies_large_cond] at mem
      rw[Ideal.mem_span_singleton'] at mem
      rcases mem with ⟨bm, eq_bm⟩
      use bm
      rw[mul_comm] at eq_bm
      use eq_bm
      intro bm' eq
      rw[← eq_bm] at eq
      rw[mul_cancel_left_mem_nonZeroDivisors] at eq
      · exact eq
      · simp only [familyPow_def, Finsupp.prod, map_prod, map_pow]
        apply prod_mem
        intro i hi
        apply pow_mem
        apply non_zero_divisor
      · exact gen

def def_unique_elem [Algebra A B] (v : F.index →₀ ℕ) (m : F.LargeIdeal^v)
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)): B :=
  (lemma_exists_in_image  F  non_zero_divisor gen v m).choose

lemma def_unique_elem_spec [Algebra A B] (v : F.index →₀ ℕ) (m : F.LargeIdeal^v)
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)):
    (algebraMap A B) (F.elem^v) * def_unique_elem F v m non_zero_divisor gen = (algebraMap A B) m := by
    apply (lemma_exists_in_image F non_zero_divisor gen v m).choose_spec.1

lemma def_unique_elem_unique  [Algebra A B] (v : F.index →₀ ℕ) (m : F.LargeIdeal^v)
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)):
    ∀ bm : B, (algebraMap A B) (F.elem^v) * bm = (algebraMap A B) m →  def_unique_elem F v m non_zero_divisor gen =bm:= by
    intro bm hbm
    apply ((lemma_exists_in_image F  non_zero_divisor gen v m).choose_spec.2 bm hbm).symm

def desc [Algebra A B]
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)) :
    A[F] →ₐ[A] B where
  toFun := Dilatation.descFun (fun x ↦ def_unique_elem F  x.pow ⟨ x.num, x.num_mem⟩  non_zero_divisor gen )
                            ( by
                              intro x y h
                              rcases h with ⟨β, hβ⟩
                              simp only
                              apply def_unique_elem_unique
                              apply_fun (fun z => (algebraMap A B) (F.elem^ (β + y.pow)) * z)
                              · simp only [mul_assoc, hβ]
                                rw[← map_mul, mul_comm _ x.num]
                                rw [hβ]
                                simp only [map_mul]
                                rw[← def_unique_elem_spec F y.pow ⟨y.num, y.num_mem⟩ non_zero_divisor gen]
                                simp only [familyPow_add, map_mul]
                                ring
                              · intro x y hx
                                simp only at hx
                                rwa [mul_cancel_left_mem_nonZeroDivisors] at hx
                                simp only [familyPow_def, Finsupp.prod, Finsupp.coe_add,
                                  Pi.add_apply, map_prod, map_pow]
                                apply prod_mem
                                intro i hi
                                apply pow_mem
                                apply non_zero_divisor)
  map_one' := by
    simp only [Dilatation.descFun, Dilatation.one_def]
    apply def_unique_elem_unique
    simp
  map_mul' := by
    intro x y
    induction x using Dilatation.induction_on with |h x =>
    induction y using Dilatation.induction_on with |h y =>
    simp only [Dilatation.descFun₂_mk_mk, Dilatation.mk_mul_mk]
    apply def_unique_elem_unique
    · exact non_zero_divisor
    · exact gen
    · simp only [Dilatation.mul'_pow, Dilatation.descFun_mk, Dilatation.mul'_num, map_mul]
      rw [familyPow_add]
      rw[← def_unique_elem_spec F  y.pow ⟨y.num, y.num_mem⟩ non_zero_divisor gen]
      rw[← def_unique_elem_spec F x.pow ⟨x.num, x.num_mem⟩ non_zero_divisor gen]
      simp only [map_mul]
      ring
  map_zero' := by
    simp only [Dilatation.descFun, Dilatation.one_def]
    apply def_unique_elem_unique
    simp
  map_add' :=  by
    intro x y
    induction x using Dilatation.induction_on with |h x =>
    induction y using Dilatation.induction_on with |h y =>
    simp only [Dilatation.descFun_mk]
    apply def_unique_elem_unique
    · exact non_zero_divisor
    · exact gen
    · simp only [Dilatation.add'_pow, Dilatation.descFun_mk, Dilatation.add'_num, map_add, map_mul]
      rw [familyPow_add]
      rw[← def_unique_elem_spec F  y.pow ⟨y.num, y.num_mem⟩ non_zero_divisor gen]
      rw[← def_unique_elem_spec F x.pow ⟨x.num, x.num_mem⟩ non_zero_divisor gen]
      simp only [map_mul]
      ring
  commutes' := by
    intro x
    simp only [Dilatation.descFun, Dilatation.one_def]
    apply def_unique_elem_unique
    simp

open Multicenter
open Dilatation
lemma dsc_spec [Algebra A B] (v : F.index →₀ ℕ) (m : F.LargeIdeal^v)
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i)):
    (algebraMap A B) (F.elem^v) * desc F non_zero_divisor gen (m/.v)  = (algebraMap A B) m := by
    apply (lemma_exists_in_image F non_zero_divisor gen v m).choose_spec.1

lemma  lemma_exists_unique_morphism [Algebra A B]
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i))
    (χ':A[F]→ₐ[A] B)  : χ' = desc F non_zero_divisor gen := by
      ext x
      induction x using induction_on with |h x =>
      have eq1 : ((algebraMap A B) (F.elem^x.pow)) *(χ' ⟨x.num, x.num_mem⟩/.x.pow) =
       (χ' (algebraMap A A[F] (F.elem^x.pow))) *(χ' ⟨x.num, x.num_mem⟩/.x.pow) := by rw[AlgHom.commutes]
      have eq2 : ((algebraMap A B) (F.elem^x.pow)) *(χ' ⟨x.num, x.num_mem⟩/.x.pow) =
       ((algebraMap A B) x.num) := by
         rw[eq1, ← map_mul]
         simp only [algebraMap_apply, mk_mul_mk, mul']
         rw[← AlgHom.commutes (χ' : A[F] →ₐ[A] B)]
         congr 1
         simp[algebraMap_apply]
         simp[mk_eq_mk]
         use 0
         simp
         simp[mul_comm]
      have eq3:  def_unique_elem F  x.pow ⟨x.num, x.num_mem⟩ non_zero_divisor gen =
         (χ' ⟨x.num, x.num_mem⟩/.x.pow) := by
          apply def_unique_elem_unique
          exact eq2
      rw[← eq3]
      rfl

lemma  lemma_exists_unique_morphism' [Algebra A B]
    (non_zero_divisor : ∀ i : F.index, (algebraMap A B) (F.elem i) ∈ nonZeroDivisors B)
    (gen : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)} = Ideal.map (algebraMap A B) (F.LargeIdeal i))
    (χ χ' :A[F]→ₐ[A] B)  : χ = χ' := by
      trans desc F non_zero_divisor gen
      · apply lemma_exists_unique_morphism
      · symm
        apply lemma_exists_unique_morphism

def ofFamilyIso {index : Type} {c : index → A} (c0 : ∀ i, c i ∈ nonZeroDivisors A) :
    A[ofFamily index c] ≃ₐ[A] A :=
  AlgEquiv.ofAlgHom
    (desc _ c0 <| by simp [LargeIdeal])
    (Algebra.ofId _ _)
    (by simp)
    (by
      refine lemma_exists_unique_morphism' (ofFamily index c) (fun i => ?_) (fun i => ?_) _ _
      · have h := Dilatation.nonzerodiv_image (F := ofFamily index c) (Finsupp.single i 1)
        rw [familyPow_single] at h
        exact h
      · have h := Dilatation.image_elem_LargeIdeal_equal (F := ofFamily index c)
          (Finsupp.single i 1)
        rw [familyPow_single, familyPow_single] at h
        exact h)

def ofEqual {F F' : Multicenter A} (eq : F = F') :
    A[F] ≃ₐ[A] A[F'] :=
  AlgEquiv.ofAlgHom
    (desc _
      (by subst eq
          intro i
          have h := Dilatation.nonzerodiv_image (F := F) (Finsupp.single i 1)
          rw [familyPow_single] at h
          exact h)
      (by subst eq
          intro i
          have h := Dilatation.image_elem_LargeIdeal_equal (F := F) (Finsupp.single i 1)
          rw [familyPow_single, familyPow_single] at h
          exact h))
    (desc _
      (by subst eq
          intro i
          have h := Dilatation.nonzerodiv_image (F := F) (Finsupp.single i 1)
          rw [familyPow_single] at h
          exact h)
      (by subst eq
          intro i
          have h := Dilatation.image_elem_LargeIdeal_equal (F := F) (Finsupp.single i 1)
          rw [familyPow_single, familyPow_single] at h
          exact h))
    (by
      subst eq
      refine lemma_exists_unique_morphism' F (fun i => ?_) (fun i => ?_) _ _
      · have h := Dilatation.nonzerodiv_image (F := F) (Finsupp.single i 1)
        rw [familyPow_single] at h
        exact h
      · have h := Dilatation.image_elem_LargeIdeal_equal (F := F) (Finsupp.single i 1)
        rw [familyPow_single, familyPow_single] at h
        exact h)
    (by
      subst eq
      refine lemma_exists_unique_morphism' F (fun i => ?_) (fun i => ?_) _ _
      · have h := Dilatation.nonzerodiv_image (F := F) (Finsupp.single i 1)
        rw [familyPow_single] at h
        exact h
      · have h := Dilatation.image_elem_LargeIdeal_equal (F := F) (Finsupp.single i 1)
        rw [familyPow_single, familyPow_single] at h
        exact h)

open Dilatation
open Multicenter
lemma reciprocal_for_univ [Algebra A B] (F : Multicenter A)
   (χ':A[F] →ₐ[A] B) : ∀ i, Ideal.span {(algebraMap A B) (F.elem i)}
         = Ideal.map (algebraMap A B) (F.LargeIdeal i):= by
          intro i
          let v : F.index →₀ ℕ := Finsupp.single i 1
          have eq1:  Ideal.span {(algebraMap A A[F]) (F.elem^v)}
             = Ideal.map (algebraMap A A[F]) (F.LargeIdeal^v):= by
             rw [image_elem_LargeIdeal_equal v]
          have eq2: F.elem^v = F.elem i := by
            simp [familyPow_def, v]
          have eq3 : F.LargeIdeal^v = F.LargeIdeal i := by
            simp [familyPow_def, v]
          have eq4: Ideal.span {(algebraMap A A[F]) (F.elem i)}
             = Ideal.map (algebraMap A A[F]) (F.LargeIdeal i):= by
                   have eq41: Ideal.span {(algebraMap A A[F]) (F.elem i)}=
                      Ideal.span {(algebraMap A A[F]) (F.elem^v)} := by
                      rw[eq2]
                   have eq42: Ideal.map (algebraMap A A[F]) (F.LargeIdeal i)=
                      Ideal.map (algebraMap A A[F]) (F.LargeIdeal^v) := by
                      rw[eq3]
                   rw[eq41, eq42, eq1]
          have eqA:  Ideal.map (algebraMap A B) (Ideal.span {F.elem i})
           = Ideal.map (algebraMap A B) (F.LargeIdeal i) := by
               have eq6: Ideal.map (algebraMap A A[F]) (Ideal.span {(F.elem i)})
                       = Ideal.span {(algebraMap A A[F]) (F.elem i)} := by
                  rw [equ_trivial_image_divisor_ring  ]
               have eq7: Ideal.map (algebraMap A A[F]) (Ideal.span {(F.elem i)})
                        =Ideal.map (algebraMap A A[F]) (F.LargeIdeal i) := by
                        rw[ eq6, ← eq4]
               have eq8: Ideal.map (χ'.toRingHom)
                          (Ideal.map (algebraMap A A[F]) (Ideal.span {(F.elem i)}))
                        =Ideal.map (χ'.toRingHom )
                          (Ideal.map (algebraMap A A[F]) (F.LargeIdeal i)) := by
                          rw[eq7]
               have eqcomp: (algebraMap A B) = (RingHom.comp χ'.toRingHom (algebraMap A A[F])) := by
                  simp
               have eq9: Ideal.map (algebraMap A B) (Ideal.span {F.elem i})
                        =Ideal.map (algebraMap A B) (F.LargeIdeal i) := by
                        rw [Ideal.map_map (algebraMap A A[F]) χ'.toRingHom] at eq8
                        rw [Ideal.map_map (algebraMap A A[F]) χ'.toRingHom] at eq8
                        simp[Function.comp_apply, Function.comp_apply] at eq8
                        exact eq8
               exact eq9

          have eqB: Ideal.map (algebraMap A B) (Ideal.span {F.elem i})=
            Ideal.span {(algebraMap A B) (F.elem i)}:= by
                rw [equ_trivial_image_divisor_ring  ]

          rw[←eqB, eqA]

open Multicenter Dilatation

@[simps]
def image_mult [Algebra A B] :  Multicenter B :=
  { index := _
    ideal i := Ideal.map (algebraMap A B) (F.ideal i)
    elem i := algebraMap A B (F.elem i)}

lemma image_mult_LargeIdeal [Algebra A B] (i : F.index):
  (image_mult (B:=B) F ).LargeIdeal i = Ideal.map (algebraMap A B) (F.LargeIdeal i) := by
  simp [LargeIdeal]
  rw[Ideal.map_sup]
  rw[Ideal.map_span]
  simp

def functo_dila_alg [Algebra A B]: A[F] →ₐ[A]  B[image_mult (B := B) F]  :=
  desc F (by
    classical
    intro i
    have h := nonzerodiv_image (F := image_mult (B := B) F ) (Finsupp.single i 1)
    simp at h
    exact h)
    (by

    intro i
    have h := image_elem_LargeIdeal_equal (F := image_mult (B := B) F) (Finsupp.single i 1)
    rw [familyPow_single, familyPow_single, image_mult_LargeIdeal, Ideal.map_map] at h
    exact h)

lemma unique_functorial_morphism_dilatation [Algebra A B]
    (other:A[F]→ₐ[A] B[image_mult (B := B) F]) : true := rfl
